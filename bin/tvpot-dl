#!/usr/bin/env perl
#
# tvpot-dl - Download flash videos from Daum tvpot
#
# Seungwon Jeong <seungwon0@gmail.com>
#
# Copyright (C) 2011 by Seungwon Jeong

use strict;

use warnings;

use 5.010;

use autodie;

use Getopt::Long;

use LWP::Simple qw< getstore is_error >;

use App::TvpotDl;

sub print_version {
    say "tvpot-dl $App::TvpotDl::VERSION";
    return;
}

sub print_help {
    print_version();

    print <<"END_HELP";

Usage:

  tvpot-dl [options] URL...

Options:

  -h, --help            print this help text and exit
  -v, --version         print program version and exit

Argument:

  URL                   URL of a tvpot video

Examples:

  tvpot-dl 'http://tvpot.daum.net/clip/ClipView.do?clipid=29772622'
  tvpot-dl 'http://music.daum.net/song/songVideo.do?songId=8443911&videoId=8446'
  tvpot-dl 'http://movie.daum.net/moviedetail/moviedetailVideoView.do?movieId=52843&videoId=29850'
  tvpot-dl 'http://tvpot.daum.net/brand/ProgramClipView.do?ownerid=P3G7gyZgrVk0&playlistid=1384392&clipid=30724905'
  tvpot-dl 'http://tvpot.daum.net/brand/ProgramClipView.do?ownerid=Sa3QtEuMXf10&playlistid=1382723&clipid=25537851'
  tvpot-dl < url_list

Please report bugs to <seungwon0\@gmail.com>.
END_HELP
    return;
}

sub download_video {
    my ($url) = @_;

    # Step 1: Get video ID
    my $video_id = App::TvpotDl::get_video_id($url);
    return if !defined $video_id;
    say "Video ID: $video_id";

    # Step 2: Get video title
    my $video_title = App::TvpotDl::get_video_title($video_id);
    return if !defined $video_title;
    say "Video title: $video_title";

    # Step 3: Get video URL
    my $video_url = App::TvpotDl::get_video_url($video_id);
    return if !defined $video_url;
    say "Video URL: $video_url";

    # Step 4: Download the video
    my $file_name = "$video_title.flv";
    say "Downloading the video as '$file_name'... "
        . '(It may takes several minutes.)';
    my $rc = getstore( $video_url, $file_name );
    if ( is_error($rc) ) {
        warn "Cannot download the video.\n";
        return;
    }

    say "Successfully downloaded '$file_name'.";

    return 1;
}

my $help;
my $version;

GetOptions( help => \$help, version => \$version )
    or die "GetOptions() failed!\n";

if ($help) {
    print_help();
    exit;
}

if ($version) {
    print_version();
    exit;
}

my @urls = @ARGV;

# tvpot-dl < url_list
if ( !-t STDIN ) {
    while ( defined( my $line = <STDIN> ) ) {
        chomp $line;

        $line =~ s/[ #] .*//xms;    # Remove comment
        $line =~ s/\s+//xmsg;       # Trim spaces

        if ( $line ne q{} ) {
            push @urls, $line;
        }
    }
}

if ( @urls < 1 ) {
    print_help();
    exit 2;
}

for my $i ( 0 .. $#urls ) {
    my $url = $urls[$i];

    printf "[%d/%d] %s\n", $i + 1, scalar @urls, $url;

    download_video($url);

    say q{};    # Blank line
}
